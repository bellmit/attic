plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'com.moowork.node' version '0.12'
    id 'com.moowork.gulp' version '0.12'
}

mainClassName = 'com.unreasonent.ds.DistantShore'
archivesBaseName = 'distant-shore'

dependencies {
    ext {
        // Ensure all dropwizard components share a version.
        dropwizardVersion = '0.9.2'

        hamcrestVersion = '1.3'
    }

    compile group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    compile group: 'io.dropwizard', name: 'dropwizard-assets', version: dropwizardVersion
    compile group: 'io.dropwizard', name: 'dropwizard-views-freemarker', version: dropwizardVersion

    compile group: 'com.loginbox.heroku', name: 'dropwizard-heroku-config', version: '1.0.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: hamcrestVersion
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: hamcrestVersion
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
}

node {
    download = true
    version = '5.8.0'
}

shadowJar {
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    mergeServiceFiles()
}

tasks.gulp_assets {
    dependsOn 'npmInstall'
}

tasks.processResources {
    dependsOn 'gulp_assets'
}

tasks.assemble {
    // The shadow JAR is a good target for cloud platforms like Heroku. `java -jar distant-shore-all.jar` is way
    // nicer than the full classpath incantation for the non-shadow JAR.
    dependsOn 'shadowJar'
}

tasks.run {
    args = ['server']
}
