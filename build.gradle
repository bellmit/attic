import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath group: 'org.ajoberstar', name: 'gradle-git', version: '0.13.0-milestone.1'
        classpath group: 'com.jfrog.bintray.gradle', name: 'gradle-bintray-plugin', version: '1.1'
    }
}

apply plugin: 'org.ajoberstar.release-opinion'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.jfrog.bintray'

group = 'com.loginbox.transactor'
sourceCompatibility = '1.8'

release {
    grgit = Grgit.open(projectDir)
}

ext {
    isSnapshot = version.toString().contains("-dev")
}

apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

dependencies {
    ext {
        hamcrestVersion = '1.3'
    }

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: hamcrestVersion
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: hamcrestVersion
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey

    publications = ['mavenJava']

    pkg {
        userOrg = 'login-box'
        repo = isSnapshot ? 'snapshots' : 'releases'
        name = 'transactor'
        websiteUrl = 'https://github.com/login-box/transactor'
        issueTrackerUrl = 'https://github.com/login-box/transactor/issues'
        vcsUrl = 'https://github.com/login-box/transactor'
        licenses = ['MIT']
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}
