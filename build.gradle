import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath group: 'org.ajoberstar', name: 'gradle-git', version: '0.13.0-milestone.1'
        classpath group: 'com.jfrog.bintray.gradle', name: 'gradle-bintray-plugin', version: '1.7.3'
    }
}

apply plugin: 'org.ajoberstar.release-opinion'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.jfrog.bintray'

group = 'ca.grimoire.dropwizard.cors'
sourceCompatibility = '1.8'

def pomConfig = {
	licenses {
		license([:]) {
			name "MIT License"
			url "http://opensource.org/licenses/MIT"
			distribution "repo"
		}
	}
}

release {
    grgit = Grgit.open(projectDir)
}

ext {
    isSnapshot = version.toString().contains("-dev")
}

apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

dependencies {
    ext {
        dropwizardVersion = '0.9.2'

        hamcrestVersion = '1.3'
    }

    compile group: 'io.dropwizard', name: 'dropwizard-db', version: dropwizardVersion

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: hamcrestVersion
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: hamcrestVersion
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
			groupId 'ca.grimoire.dropwizard.cors'
            artifactId 'dropwizard-simple-cors'
            version = project.version
			pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Dropwizard Simple CORS')
                root.appendNode('url', 'https://github.com/ojacobson/dropwizard-simple-cors')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey

    publications = ['mavenJava']

    pkg {
        userOrg = 'ojacobson'
        repo = isSnapshot ? 'snapshots' : 'releases'
        name = 'dropwizard-simple-cors'
        websiteUrl = 'https://github.com/ojacobson/dropwizard-simple-cors'
        issueTrackerUrl = 'https://github.com/ojacobson/dropwizard-simple-cors/issues'
        vcsUrl = 'https://github.com/ojacobson/dropwizard-simple-cors'
        licenses = ['MIT']
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}
