buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: '1.2.1'
        classpath group: 'com.moowork.gradle', name: 'gradle-node-plugin', version: '0.8'
        classpath group: 'com.moowork.gradle', name: 'gradle-gulp-plugin', version: '0.9'
    }
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.moowork.node'
apply plugin: 'com.moowork.gulp'
apply plugin: 'idea'

group = 'com.loginbox.app'
mainClassName = 'com.loginbox.app.LoginBox'

sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    acceptanceTest {
        compileClasspath = files(sourceSets.main.output, configurations.acceptanceTestCompile)
        runtimeClasspath = files(output, sourceSets.main.output, configurations.acceptanceTestRuntime)
    }
}

configurations {
    acceptanceTestCompile.extendsFrom compile, testCompile
    acceptanceTestRuntime.extendsFrom runtime, testRuntime, acceptanceTestCompile
}

dependencies {
    ext {
        dropwizardVersion = '0.8.0'

        // The following versions have to track dropwizardVersion but are not equal to it. You
        // can obtain the correct version numbers from the published dropwizard-parent POM.
        jerseyVersion = '2.16'

        // End of Dropwizard-related versions. The following versions can vary independently of Dropwizard.
        hamcrestVersion = '1.3'
        seleniumVersion = '2.45.0'
    }

    compile group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    compile group: 'io.dropwizard', name: 'dropwizard-assets', version: dropwizardVersion
    compile group: 'com.loginbox.heroku', name: 'dropwizard-heroku-config', version: '0.1.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: hamcrestVersion
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: hamcrestVersion
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'

    acceptanceTestCompile group: 'com.lewisd', name: 'simple-dsl', version: '0.2'
    acceptanceTestCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion
    acceptanceTestCompile group: 'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version: seleniumVersion
    acceptanceTestCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: seleniumVersion
}

node {
    download = true
    version = '0.10.35'
}

shadowJar {
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    mergeServiceFiles()
}

idea {
    module {
        testSourceDirs += sourceSets.acceptanceTest.allJava.srcDirs
        scopes.TEST.plus += [
                configurations.acceptanceTestCompile,
                configurations.acceptanceTestRuntime
        ]
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:all'
}

tasks.gulp_assets {
    dependsOn 'npmInstall'
}

tasks.processResources {
    dependsOn 'gulp_assets'
}

tasks.assemble {
    dependsOn 'shadowJar'
}

task acceptanceTest(type: Test) {
    description 'Run acceptance tests.'
    group 'verification'

    dependsOn 'acceptanceTestClasses'
    shouldRunAfter 'test'

    inputs.dir sourceSets.main.output.dirs
    inputs.dir sourceSets.acceptanceTest.output.dirs

    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath = sourceSets.acceptanceTest.runtimeClasspath
}

task stage {
    dependsOn 'shadowJar'
}

tasks.run {
    args = ['server']
}

tasks.check {
    dependsOn 'acceptanceTest'
}
